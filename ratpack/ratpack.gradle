import io.ratpack.gradle.RunInBackground

apply from: "gradle/idea/idea.gradle"

buildscript {
  repositories {
    maven { url "http://oss.jfrog.org/artifactory/repo" }
    jcenter()
  }
  dependencies {
    classpath 'io.ratpack:ratpack-gradle:0.9.1-SNAPSHOT'
  }
}

allprojects {
  apply plugin: 'idea'

  apply from: rootProject.file("gradle/dependencies.gradle")
}

subprojects {
  apply plugin: "ratpack"

  repositories {
    mavenLocal()
    maven { url "http://oss.jfrog.org/artifactory/repo" }
    jcenter()
    maven { url "http://repo.springsource.org/repo" }
    maven { url "https://nexus.codehaus.org/content/repositories/snapshots/" }
  }

  configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
  }

  if (name in ['ratpack-java', 'ratpack-groovy']) {
    dependencies {
      springloaded "org.springsource.springloaded:springloaded-core:1.1.4"
      compile "io.ratpack:ratpack-jackson:${versions.ratpack}",
        project(':common')
      runtime 'mysql:mysql-connector-java:5.1.28'
      testCompile libraries.spock,
        project(':common-test')
    }

    task runInBackground(type: RunInBackground) {
      dependsOn installApp

      directory installApp.outputs.files.singleFile
      command "bin/${project.name}"
      readyText 'Ratpack started'
    }
  }
}
